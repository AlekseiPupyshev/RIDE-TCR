{-# STDLIB_VERSION 3 #-}
{-# CONTENT_TYPE DAPP #-}
{-# SCRIPT_TYPE ACCOUNT #-}

# TCR implementation with commit-reveal scheme
# broadcast(invokeScript({dappAddress: address(env.accounts[6]), call:{function:"setOrderLimitSell", args:[{type:"string", value: "19_592006_3MqyL7JckZ9hC4G2fU2JPtKSXppQF27VCsr"}, {type:"integer", value: "55"}, {type:"integer", value: 5}]}, payment: []}))

let maxVoters = 3
let majorityCnt = 2
let newItemFee = 50000000
let voteItemFee = 15000000

@Callable(i)
func deposit() = {
   let pmt = extract(i.payment)
   if (isDefined(pmt.assetId)) then throw("can hodl waves only at the moment")
   else {
        let currentKey = toBase58String(i.caller.bytes)
        let currentAmount = match getInteger(this, currentKey) {
            case a:Int => a
            case _ => 0
        }
        let newAmount = currentAmount + pmt.amount
        WriteSet([
            DataEntry(currentKey, newAmount)
        ])
   }
}

@Callable(i)
func withdraw(amount: Int) = {
        let currentKey = toBase58String(i.caller.bytes)
        let currentAmount = match getInteger(this, currentKey) {
            case a:Int => a
            case _ => 0
        }
        let newAmount = currentAmount - amount
     if (amount < 0)
            then throw("Can't withdraw negative amount")
    else if (newAmount < 0)
            then throw("Not enough balance")
            else ScriptResult(
                    WriteSet([DataEntry(currentKey, newAmount)]),
                    TransferSet([ScriptTransfer(i.caller, amount, unit)])
                )
    }

@Callable(i)
func addItem(itemId: String) = {
    let currentAcc = toBase58String(i.caller.bytes)
    let currentAmount = match getInteger(this, currentAcc) {
            case a:Int => a
            case _ => 0
    }
    let noMark = "no"
    let item = match getString(this, currentAcc) {
            case a:String => a
            case _ => noMark
    }
    if (currentAmount < newItemFee)
        then throw("Not enough funds to add new item")
    else if (item != noMark)
        then throw("An item is already exist")
    else WriteSet([
        DataEntry(currentAcc, currentAmount - newItemFee),
        DataEntry("author_" + itemId, currentAcc),
        DataEntry("block_" + itemId, height),
        DataEntry("bank_" + itemId, newItemFee),
        DataEntry("status_" + itemId, "new")
    ])
}

@Callable(i)
func voteCommit(itemId: String, voteHash: String) = {
    let currentAcc = toBase58String(i.caller.bytes)
    let currentAmount = match getInteger(this, currentAcc) {
            case a:Int => a
            case _ => 0
    }
    let noMark = "no"
    let bankKey = "bank_" + itemId
    let bank = match getInteger(this, bankKey) {
            case a:Int => a
            case _ => 0
    }
    let statusKey = "status_" + itemId
    let status = match getString(this, statusKey) {
            case a:String => a
            case _ => noMark
    }
    let nCommitsKey = "ncommits_" + itemId
    let nCommits = match getInteger(this, nCommitsKey) {
            case a:Int => a
            case _ => 0
    }
    let commitKey = "commit_" + itemId + "_" + currentAcc
    let commit = match getString(this, commitKey) {
            case a:String => a
            case _ => noMark
    }
    if (currentAmount < voteItemFee)
        then throw("Not enough funds to vote for a new item")
    else if (status == noMark)
        then throw("An item doesn't exist")
    else if (status != "new" && status != "voting_commit")
        then throw("Wrong item status for 'commit' action")
    else if (nCommits >= maxVoters)
        then throw("No more voters for this item")
    else if (commit != noMark)
        then throw("Can't vote twice")
    else WriteSet([
        DataEntry(currentAcc, currentAmount - voteItemFee),
        DataEntry(bankKey, bank + voteItemFee),
        DataEntry(statusKey, "voting_commit"),
        DataEntry(commitKey, voteHash),
        DataEntry(nCommitsKey, nCommits + 1)
    ])
}

@Callable(i)
func voteReveal(itemId: String, voteResult: String, salt: String) = {
    let preCommit = voteResult + salt
    let rideCommitHash = toBase58String(sha256(toBytes(preCommit)))
    let currentAcc = toBase58String(i.caller.bytes)
    let noMark = "no"
    let commitKey = "commit_" + itemId + "_" + currentAcc
    let voteHash = match getString(this, commitKey) {
            case a:String => a
            case _ => noMark
    }
    let statusKey = "status_" + itemId
    let status = match getString(this, statusKey) {
            case a:String => a
            case _ => noMark
    }
    let revealKey = "reveal_" + itemId + "_" + currentAcc
    let reveal = match getString(this, revealKey) {
            case a:String => a
            case _ => noMark
    }
    let yesKey = "cnt_yes_" + itemId
    let yesCnt = match getInteger(this, yesKey) {
            case a:Int => a
            case _ => 0
    }
    let noKey = "cnt_no_" + itemId
    let noCnt = match getInteger(this, noKey) {
            case a:Int => a
            case _ => 0
    }
    let yesDelta = if(voteResult == "1") then 1 else 0
    let noDelta = if(voteResult == "0") then 1 else 0
    if (voteHash == noMark)
        then throw("There is no commit yet")
    else if (voteHash != rideCommitHash)
        then throw("Hashes don't match")
    else if (status != "voting_commit" && status != "voting_reveal")
        then throw("Wrong item status for 'reveal' action")
    else if (reveal != noMark)
        then throw("Can't vote twice")
    else if (voteResult != "1" && voteResult != "0")
        then throw("Bad vote result format")
    else WriteSet([
        DataEntry(statusKey, "voting_reveal"),
        DataEntry(revealKey, voteResult),
        DataEntry(yesKey, yesCnt + yesDelta),
        DataEntry(noKey, noCnt + noDelta)
    ])
}

@Callable(i)
func checkResults(itemId: String, account: String) = {
    let statusKey = "status_" + itemId
    let revealKey = "reveal_" + itemId + "_" + account
    let rewardKey = "reward_" + itemId + "_" + account
    let noMark = "no"
    let reward = match getString(this, rewardKey) {
            case a:String => a
            case _ => noMark
    }
    let reveal = match getString(this, revealKey) {
            case a:String => a
            case _ => noMark
    }
    let yesKey = "cnt_yes_" + itemId
    let yesCnt = match getInteger(this, yesKey) {
            case a:Int => a
            case _ => 0
    }
    let noKey = "cnt_no_" + itemId
    let noCnt = match getInteger(this, noKey) {
            case a:Int => a
            case _ => 0
    }
    let currentAmount = match getInteger(this, account) {
            case a:Int => a
            case _ => 0
    }
    let finalStatus = if ( yesCnt >= majorityCnt ) then "accepted" else "declined"
    let isWinner = if( ( reveal == "1" && yesCnt >= majorityCnt ) || ( reveal == "0" && noCnt >= majorityCnt ) ) then 1 else 0
    let rewardStatus = if ( isWinner == 1 ) then "winner" else "looser"
    let nWinners = if ( yesCnt >= majorityCnt ) then yesCnt else noCnt
    let nLoosers = maxVoters - nWinners
    let profit = isWinner*(voteItemFee + (nLoosers*voteItemFee + newItemFee)/nWinners)
    if (yesCnt + noCnt < maxVoters)
        then throw("The voting hasn't finished yet")
    else if (reveal == noMark)
        then throw("Account hasn't participated in voting")
    else if (reward != noMark)
        then throw("This account has been checked")
    else WriteSet([
        DataEntry(statusKey, finalStatus),
        DataEntry(account, currentAmount + profit),
        DataEntry(rewardKey, rewardStatus)
    ])
}
